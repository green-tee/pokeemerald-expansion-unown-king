mapscripts MtPyre_Exterior_MapScripts {
    MAP_SCRIPT_ON_RESUME: MtPyre_Exterior_EventScript_TryRemoveLatiosOrLatias
    MAP_SCRIPT_ON_TRANSITION: MtPyre_Exterior_EventScript_TryShowLatiosLatias
}

const VAR_LATIOS_OR_LATIAS = VAR_TEMP_0

script MtPyre_Exterior_EventScript_TryRemoveLatiosOrLatias {
    if (flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
        specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_CAUGHT || var(VAR_RESULT) == B_OUTCOME_WON) {
            removeobject(VAR_LAST_TALKED)
        }
    }
}

script MtPyre_Exterior_EventScript_TryShowLatiosLatias {
    if (flag(FLAG_HIDE_LATIOS) && !flag(FLAG_CAUGHT_LATIOS)) {
        clearflag(FLAG_HIDE_LATIOS)
    }
    if (flag(FLAG_HIDE_LATIAS) && !flag(FLAG_CAUGHT_LATIAS)) {
        clearflag(FLAG_HIDE_LATIAS)
    }
}

script MtPyre_Exterior_EventScript_LatiosOrLatias {
    lock
    faceplayer
    call(MtPyre_Exterior_EventScript_LatiosOrLatias_Cry)
    msgbox(MtPyre_Exterior_Text_FightLatiosOrLatias, MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        closemessage
        setflag(FLAG_SYS_CTRL_OBJ_DELETE)
        call(MtPyre_Exterior_EventScript_LatiosOrLatias_SetBattle)
        special(BattleSetup_StartLegendaryBattle)
        waitstate
        clearflag(FLAG_SYS_CTRL_OBJ_DELETE)
        specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_CAUGHT) {
            call(MtPyre_Exterior_EventScript_LatiosOrLatias_CaughtFlag)
            call(MtPyre_Exterior_EventScript_LatiosOrLatias_SoulDew)
            //giveitem(ITEM_SOUL_DEW, 1)
        }
    } else {
        closemessage
    }
    release
}

script(local) MtPyre_Exterior_EventScript_LatiosOrLatias_Cry {
    switch (var(VAR_LAST_TALKED)) {
        case LOCALID_LATIOS:
            playmoncry(SPECIES_LATIOS, CRY_MODE_NORMAL)
        case LOCALID_LATIAS:
            playmoncry(SPECIES_LATIAS, CRY_MODE_NORMAL)
    }
}

script(local) MtPyre_Exterior_EventScript_LatiosOrLatias_SetBattle {
    switch (var(VAR_LAST_TALKED)) {
        case LOCALID_LATIOS:
            setwildbattle(SPECIES_LATIOS, 45, item=ITEM_MAX_REVIVE)
        case LOCALID_LATIAS:
            setwildbattle(SPECIES_LATIAS, 45, item=ITEM_MAX_REVIVE)
        default:
            setwildbattle(SPECIES_HAXORUS, 45, item=ITEM_MAX_REVIVE) // It should never happen, but it's a failsafe
    }
}

script(local) MtPyre_Exterior_EventScript_LatiosOrLatias_CaughtFlag {
    switch (var(VAR_LAST_TALKED)) {
        case LOCALID_LATIOS:
            setflag(FLAG_CAUGHT_LATIOS)
        case LOCALID_LATIAS:
            setflag(FLAG_CAUGHT_LATIAS)
    }
}

script(local) MtPyre_Exterior_EventScript_LatiosOrLatias_SoulDew {
    switch (var(VAR_LAST_TALKED)) {
        case LOCALID_LATIOS:
            clearflag(FLAG_ITEM_LATIOS_SOUL_DEW)
            addobject(LOCALID_LATIOS_SOUL_DEW, MAP_MT_PYRE_EXTERIOR)
        case LOCALID_LATIAS:
            clearflag(FLAG_ITEM_LATIAS_SOUL_DEW)
            addobject(LOCALID_LATIAS_SOUL_DEW, MAP_MT_PYRE_EXTERIOR)
    }
}

text MtPyre_Exterior_Text_FightLatiosOrLatias {
    "This Pok√©mon is staring at you\n"
    "with a smile on its face.\l"
    "Will you fight it?"
}

script MtPyre_Exterior_EventScript_GoToFinalBoss {
    lockall
    message(MtPyre_Exterior_Text_WantToGoToFinalBoss)
    waitmessage
    dynmultichoice(16, 8, TRUE, 2, 1, DYN_MULTICHOICE_CB_NONE, Multichoice_Option_GoAhead, Multichoice_Option_GoBack)
    if (var(VAR_RESULT) == 0) {
        // Warp and start final boss sequence
        closemessage
        msgbox(MtPyre_Exterior_Text_GoodLuck, MSGBOX_DEFAULT)
        closemessage
        applymovement(LOCALID_PLAYER, MtPyre_Exterior_Movement_GoForth)
        waitmovement(LOCALID_PLAYER)
        warp(MAP_MT_PYRE_SUMMIT, 15, 30)
        waitstate
    } else {
        // Walk a few steps down
        closemessage
        applymovement(LOCALID_PLAYER, MtPyre_Exterior_Movement_GoBack)
        waitmovement(LOCALID_PLAYER)
        releaseall
    }
}

text MtPyre_Exterior_Text_WantToGoToFinalBoss {
    "Hold on a second!\n"
    "Ahead lies the end of your quest.\p"
    "Once you cross this line, you won't be\n"
    "able to come back. The only options are\l"
    "victory or defeat.\p"
    "Losing the final battle will take you\n"
    "to the title screen.\p"
    "If you wish to save your game, go back\n"
    "before stepping forward.\p"
    "What will you do?"
}

text Multichoice_Option_GoAhead {
    "I'm going ahead!"
}

text Multichoice_Option_GoBack {
    "I'm not ready."
}

text MtPyre_Exterior_Text_GoodLuck {
    "Good luck!"
}

movement MtPyre_Exterior_Movement_GoForth {
    walk_up
}

movement MtPyre_Exterior_Movement_GoBack {
    walk_down * 2
}
