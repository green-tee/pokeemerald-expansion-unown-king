mapscripts FieryPath_MapScripts {
    MAP_SCRIPT_ON_RESUME: FieryPath_EventScript_TryRemoveFakeBoulder
}

const VAR_EGG_OPTION_1 = VAR_TEMP_0
const VAR_EGG_OPTION_2 = VAR_TEMP_1
const VAR_EGG_CHOSEN_SPECIES = VAR_TEMP_2

script FieryPath_EventScript_TryRemoveFakeBoulder {
    if (flag(FLAG_SYS_CTRL_OBJ_DELETE)) {
        specialvar(VAR_RESULT, GetBattleOutcome)
        if (var(VAR_RESULT) == B_OUTCOME_CAUGHT || var(VAR_RESULT) == B_OUTCOME_WON) {
            removeobject(VAR_LAST_TALKED)
        }
    }
}

script FieryPath_EventScript_Inscription {
    lock
    msgbox(FieryPath_Text_WaysOfWater, MSGBOX_UNOWNCRYPT)
    closemessage
    release
}

text FieryPath_Text_WaysOfWater {
    "Only those who truly master the ways\n"
    "of water can explore this temple\l"
    "to the fullest.\p"
    "This hallway is connected to one\n"
    "of the aquatic caverns."
}

script FieryPath_EventScript_MaidenGaia {
    lock
    faceplayer
    if (flag(FLAG_BADGE04_GET)) {
        if (flag(FLAG_RECEIVED_EGG_FROM_GAIA)) {
            msgbox(FieryPath_Text_HallwayImpractical, MSGBOX_DEFAULT)
        } else {
            if (flag(FLAG_GAVE_WATER_TO_GAIA)) {
                call(FieryPath_EventScript_TryGiveEgg)
            } else {
                msgbox(FieryPath_Text_ImSoThirsty, MSGBOX_DEFAULT)
                checkitem(ITEM_FRESH_WATER, 1)
                if (var(VAR_RESULT) == TRUE) {
                    msgbox(FieryPath_Text_MayIHaveWater, MSGBOX_YESNO)
                    if (var(VAR_RESULT) == YES) {
                        removeitem(ITEM_FRESH_WATER, 1)
                        setflag(FLAG_GAVE_WATER_TO_GAIA)
                        msgbox(FieryPath_Text_ThankYouVeryMuch, MSGBOX_DEFAULT)
                        closemessage
                        call(FieryPath_EventScript_TryGiveEgg)
                    } else {
                        closemessage
                    }
                } else {
                    closemessage
                }
            }
        }
    } else {
        msgbox(FieryPath_Text_ImOneOfTheMaidens, MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            msgbox(FieryPath_Text_LetTheBattleBegin, MSGBOX_DEFAULT)
            closemessage
            playbgm(MUS_VS_GYM_LEADER, FALSE)
            trainerbattle_no_intro(TRAINER_MAIDEN3, FieryPath_Text_MaidenGaiaDefeated)
            lock
            faceplayer
            msgbox(FieryPath_Text_YouSucceeded, MSGBOX_DEFAULT)
            closemessage
            giveitem(ITEM_HM_STRENGTH)
            setflag(FLAG_BADGE04_GET)
            msgbox(FieryPath_Text_YouCanReachTheStrongestSister, MSGBOX_DEFAULT)
            closemessage
        } else {
            msgbox(FieryPath_Text_IUnderstand, MSGBOX_DEFAULT)
            closemessage
        }
    }
    release
}

script(local) FieryPath_EventScript_TryGiveEgg {
    getpartysize
    if (var(VAR_RESULT) < PARTY_SIZE) {
        switch (var(VAR_STARTER_MON)) {
            case 0: // Electrike
                setvar(VAR_EGG_OPTION_1, SPECIES_HOUNDOUR)
                setvar(VAR_EGG_OPTION_2, SPECIES_VULPIX_ALOLA)
            case 1: // Houndour
                setvar(VAR_EGG_OPTION_1, SPECIES_VULPIX_ALOLA)
                setvar(VAR_EGG_OPTION_2, SPECIES_ELECTRIKE)
            case 2: // Vulpix (Alola)
                setvar(VAR_EGG_OPTION_1, SPECIES_ELECTRIKE)
                setvar(VAR_EGG_OPTION_2, SPECIES_HOUNDOUR)
        }
        bufferspeciesname(STR_VAR_1, VAR_EGG_OPTION_1)
        bufferspeciesname(STR_VAR_2, VAR_EGG_OPTION_2)
        message(FieryPath_Text_WhichEggDoYouWant)
        waitmessage
        dynmultichoice(20, 8, TRUE, 2, 0, DYN_MULTICHOICE_CB_NONE, "{STR_VAR_1}", "{STR_VAR_2}")
        if (var(VAR_RESULT) == 0) {
            copyvar(VAR_EGG_CHOSEN_SPECIES, VAR_EGG_OPTION_1)
        } else {
            copyvar(VAR_EGG_CHOSEN_SPECIES, VAR_EGG_OPTION_2)
        }
        msgbox(FieryPath_Text_Sure, MSGBOX_DEFAULT)
        giveegg(VAR_EGG_CHOSEN_SPECIES)
        if (var(VAR_RESULT) != MON_CANT_GIVE) {
            setflag(FLAG_RECEIVED_EGG_FROM_GAIA)
            copyvar(VAR_GAIA_EGG_SPECIES, VAR_EGG_CHOSEN_SPECIES)
        }
        playfanfare(MUS_OBTAIN_ITEM)
        msgbox(FieryPath_Text_ReceivedAnEgg, MSGBOX_DEFAULT)
        waitfanfare
        closemessage
    } else {
        msgbox(FieryPath_Text_NoRoomForEgg, MSGBOX_DEFAULT)
        closemessage
    }
}

text FieryPath_Text_ImOneOfTheMaidens {
    "Hi, traveler!\n"
    "Do you need help?\p"
    "I'm Gaia, one of the four Maidens\n"
    "of this temple.\p"
    "I can lend you the power to move\n"
    "those heavy boulders out of your way,\l"
    "but you must defeat me in battle first.\p"
    "I'm not the strongest of the four,\n"
    "but I advise you to be prepared\l"
    "nonetheless.\p"
    "Are you sure you want to challenge me?"
}

text FieryPath_Text_IUnderstand {
    "I understand.\n"
    "If you want to prove yourself,\l"
    "talk to me again."
}

text FieryPath_Text_LetTheBattleBegin {
    "Of course.\n"
    "Let the battle begin!"
}

text FieryPath_Text_MaidenGaiaDefeated {
    "You defeated me. For that, you deserve\n"
    "the prize I'm about to give you."
}

text FieryPath_Text_YouSucceeded {
    "You succeeded in winning our battle.\n"
    "That makes you worthy of receiving\l"
    "this power."
}

text FieryPath_Text_YouCanReachTheStrongestSister {
    "With this power, you can reach\n"
    "the strongest of my Sisters.\p"
    "The large boulders on the floor are\n"
    "now barely an inconvenience to you.\p"
    "I wish you success in everything\n"
    "in your life!"
}

text FieryPath_Text_ImSoThirsty {
    "Oh, I'm so thirsty!"
}

text FieryPath_Text_MayIHaveWater {
    "Hey, may I have a bottle of water,\n"
    "please?"
}

text FieryPath_Text_ThankYouVeryMuch {
    "Thank you very much!\p"
    "{FONT_SMALL}Glug… Glug…\p"
    "{FONT_NORMAL}That's better!\p"
    "Hey, listen!"
}

text FieryPath_Text_NoRoomForEgg {
    "I've got two Pokémon eggs here,\n"
    "but your party is already full of\l"
    "Pokémon, you have no room for an egg.\p"
    "Could you come back with a free space?"
}

text FieryPath_Text_WhichEggDoYouWant {
    "I've got two Pokémon eggs here.\n"
    "One will hatch into {STR_VAR_1},\l"
    "the other into {STR_VAR_2}.\p"
    "Which one would you like?"
}

text FieryPath_Text_Sure {
    "Sure!"
}

text FieryPath_Text_HallwayImpractical {
    "This hallway is highly impractical,\n"
    "don't you think?"
}

text FieryPath_Text_ReceivedAnEgg {
    "{PLAYER} received an egg, which\n"
    "will hatch into {STR_VAR_1}."
}

script FieryPath_EventScript_BeastTamerNE {
    lock
    faceplayer
    if (flag(FLAG_TEMP_1)) {
        msgbox(FieryPath_Text_ImUpToAnotherFight)
        closemessage
    } else {
        msgbox(FieryPath_Text_ImTheStrongestTrainer, MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
            playbgm(MUS_VS_GYM_LEADER, FALSE)
            msgbox(FieryPath_Text_VeryWellBringItOn, MSGBOX_DEFAULT)
            closemessage
            trainerbattle_no_intro(TRAINER_BEAST_TAMER_CAESAR, FieryPath_Text_YouAreTheRealBeast)
            setflag(FLAG_TEMP_1)
            if (flag(FLAG_RECEIVED_MASTER_BALL)) {
                msgbox(FieryPath_Text_IHaveNothingMoreToSay, MSGBOX_DEFAULT)
                closemessage
            } else {
                msgbox(FieryPath_Text_IHaveAPresent, MSGBOX_DEFAULT)
                closemessage
                giveitem(ITEM_MASTER_BALL, 1)
                setflag(FLAG_RECEIVED_MASTER_BALL)
                msgbox(FieryPath_Text_ThisIsAMasterBall, MSGBOX_DEFAULT)
                closemessage
            }
        } else {
            closemessage
        }
    }
    release
}

text FieryPath_Text_ImTheStrongestTrainer {
    "I'm the strongest Trainer lurking\n"
    "in this temple. My Pokémon are brutal\l"
    "and well trained. They know moves that\l"
    "you can only dream of teaching yours.\p"
    "If you want to challenge me, I'm in."
}

text FieryPath_Text_VeryWellBringItOn {
    "Very well, traveler.\n"
    "I don't intend to go easy on you.\p"
    "Now, bring it on!"
}

text FieryPath_Text_YouAreTheRealBeast {
    "You are the real beast!"
}

text FieryPath_Text_IHaveNothingMoreToSay {
    "Traveler! I have nothing more to say\n"
    "to you.\p"
    "Don't let any opponent frighten you!"
}

text FieryPath_Text_IHaveAPresent {
    "Excellent, traveler!\n"
    "You fight incredibly well!\p"
    "I have a present for you.\n"
    "You absolutely earned it!"
}

text FieryPath_Text_ThisIsAMasterBall {
    "This is a Master Ball.\n"
    "It's the most effective Poké Ball.\p"
    "It can catch any wild Pokémon\n"
    "and it never fails.\p"
    "Don't go waste it on a common Pokémon!"
}

text FieryPath_Text_ImUpToAnotherFight {
    "If you pass by again,\n"
    "I'm up for another fight!"
}

script FieryPath_EventScript_BeastTamerNW {
    trainerbattle_single(TRAINER_BEAST_TAMER_TRITON, FieryPath_Text_AssistMeInTraining, FieryPath_Text_NotAsPlanned)
    msgbox(FieryPath_Text_LegendaryPokemon, MSGBOX_AUTOCLOSE)
}

text FieryPath_Text_AssistMeInTraining {
    "You!\n"
    "Assist me in my training!"
}

text FieryPath_Text_NotAsPlanned {
    "Hmpf!\n"
    "Didn't work out quite as planned!"
}

text FieryPath_Text_LegendaryPokemon {
    "I know there's supposed to be a\n"
    "legendary creature inside the\l"
    "Room of Bagon, but last time I checked,\l"
    "it wasn't there."
}

script FieryPath_EventScript_BeastTamerSW {
    trainerbattle_single(TRAINER_BEAST_TAMER_WILHELM, FieryPath_Text_ThisIsntAPlaceForInexperienced, FieryPath_Text_ITakeItBack)
    msgbox(FieryPath_Text_HowIsItGoing, MSGBOX_AUTOCLOSE)
}

text FieryPath_Text_ThisIsntAPlaceForInexperienced {
    "This isn't a place for the inexperienced!"
}

text FieryPath_Text_ITakeItBack {
    "I take it back, you aren't inexperenced\n"
    "at all."
}

text FieryPath_Text_HowIsItGoing {
    "How's it going, champ?"
}

script FieryPath_EventScript_BeastTamerSE {
    trainerbattle_single(TRAINER_BEAST_TAMER_SOLOMON, FieryPath_Text_TodaysYouthIsPathetic, FieryPath_Text_YouGotLucky)
    msgbox(FieryPath_Text_HaveYouFoundDarkrai, MSGBOX_AUTOCLOSE)
}

text FieryPath_Text_TodaysYouthIsPathetic {
    "Today's youth is pathetic!\n"
    "I'll show you how a real Trainer fights!"
}

text FieryPath_Text_YouGotLucky {
    "You got lucky this time!"
}

text FieryPath_Text_HaveYouFoundDarkrai {
    "Youngling!\n"
    "Have you found Darkrai yet?\p"
    "I heard it patrols a room so dark\n"
    "that you can't see your own feet.\p"
    "Of course, against my Bug-type Pokémon,\n"
    "it's got no chance!"
}
